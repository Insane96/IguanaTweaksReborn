plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = "${mod_version}-mc${mc_version}"
group = 'insane96mcp.iguanatweaksreborn'

base {
    archivesName = 'IguanaTweaksReborn'
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

jarJar.enable()
minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${mc_version}"

    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '-mixin.config=iguanatweaksreborn.mixins.json'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            workingDirectory project.file('run')
            property 'mixin.debug.export', 'true'
        }
        client2 {
            workingDirectory project.file('run-2')
            args "--username", "Dev2"
        }

        server {
            workingDirectory project.file('run-server')
            args '--nogui'
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "iguanatweaksreborn.refmap.json"
}

repositories {
    flatDir {
        dir 'libs'
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
        content {
            includeGroup "dev.gigaherz.sewingkit"
        }
    }
    maven {
        url = "https://jitpack.io"
        content {
            includeGroup "com.github.Virtuoel"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://maven.minecraftforge.net/' }

}

dependencies {
    minecraft "net.neoforged:forge:${mc_version}-${neo_version}"

    implementation fg.deobf("blank:InsaneLib:${lib_version}-mc${mc_version}")
    implementation fg.deobf("blank:Stamina:1.1.1")

    implementation fg.deobf("curse.maven:selene-499980:5254462")
    runtimeOnly fg.deobf("curse.maven:mmmmmmmmmmmm-225738:5051316")
    runtimeOnly fg.deobf("curse.maven:better-advancements-272515:4631253")
    // runtimeOnly fg.deobf("curse.maven:worldedit-225608:4586218")
    implementation fg.deobf("curse.maven:supplementaries-412082:5225134")

    implementation fg.deobf("com.github.glitchfiend:SereneSeasons:${mc_version}-${sereneseasons_version}")
    implementation fg.deobf("curse.maven:time-control-303570:5095704")

    implementation fg.deobf("curse.maven:global-gamerules-227657:4587490")

    compileOnly fg.deobf("dev.emi:emi-forge:${emi_version}:api")
    runtimeOnly fg.deobf("dev.emi:emi-forge:${emi_version}")
    implementation fg.deobf("curse.maven:jei-238222:5101366")

    implementation fg.deobf("com.github.Virtuoel:Pehkui:${pehkui_version}-${mc_version}-forge")
    //implementation fg.deobf("curse.maven:kiwi-303657:5603816")
    //implementation fg.deobf("curse.maven:passable-foliage-379887:4869702")
    implementation fg.deobf("curse.maven:tool-belt-260262:4581167")
    //Needed for data gen, Hardcoded MC version as there is no 1.20.1 version
    runtimeOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-1.20:1.8.0")

    runtimeOnly fg.deobf("net.darkhax.maxhealthfix:MaxHealthFix-Forge-${mc_version}:${max_health_fix_version}")

    //runtimeOnly fg.deobf("curse.maven:corail-woodcutter-331983:4685515")

    //implementation fg.deobf("curse.maven:puzzles-lib-495476:4660882")
    //runtimeOnly fg.deobf("curse.maven:easy-magic-456239:4658750")
    implementation fg.deobf("curse.maven:blueprint-382216:5228040")
    implementation fg.deobf("curse.maven:autumnity-365045:5229021")
    implementation fg.deobf("curse.maven:berry-good-355589:5228437")
    implementation fg.deobf("curse.maven:allurement-438003:5228059")
    implementation fg.deobf("curse.maven:farmers-delight-398521:5051242")

    implementation fg.deobf("curse.maven:zeta-968868:5418213")
    implementation fg.deobf("curse.maven:quark-243121:5418252")

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    minecraftLibrary 'com.ezylang:EvalEx:3.0.2'
    jarJar("com.ezylang:EvalEx:3.2.0") {
        jarJar.ranged(it, "[3.2.0,)")
    }

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.5"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.3.5")) {
        jarJar.ranged(it, "[0.3.5,)")
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : "iguanatweaksreborn",
                "Specification-Vendor"    : "Insane96MCP",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Insane96MCP",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "iguanatweaksreborn.mixins.json"
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
    archiveClassifier = 'no-jar-jar'
}

tasks.named('jarJar') {
    archiveClassifier = ''
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}